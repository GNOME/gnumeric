AC_INIT(src/gnumeric.h)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnumeric,0.47)
AM_MAINTAINER_MODE
AM_ACLOCAL_INCLUDE(macros)

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_PROG_YACC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

GNOME_INIT

## this should come after `AC_PROG_CC'
GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS
GNOME_XML_CHECK

dnl
dnl alloca tests
dnl;
AC_FUNC_ALLOCA
if test $ac_cv_header_alloca_h = yes; then
        GNUMERIC_HAVE_ALLOCA_H=1
else
        GNUMERIC_HAVE_ALLOCA_H=0
fi
AC_SUBST(GNUMERIC_HAVE_ALLOCA_H)

AC_SUBST(QTTHREADS_LIB)
AC_SUBST(TERMCAP_LIB)
AC_SUBST(READLINE_LIB)
AC_SUBST(GUILE_LIBS)
AC_SUBST(GUILE_INCS)
AC_ARG_WITH(guile,[--with-guile   Include Guile support],[GNOME_CHECK_GUILE])

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl **************************************************
dnl * internationalization support
dnl **************************************************
dnl 
dnl
dnl Check doc/translating.sgml for a description of how to translate
dnl and why we have so many translations.
dnl
ALL_LINGUAS="cs da de en_GB es es_DO es_GT es_HN es_MX es_PA es_PE es_SV et fi fr hu hr it ja ko nl no pt pt_BR ru pl sk zh_TW.Big5 sv uk"
AM_GNU_GETTEXT
AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)


dnl
dnl On Solaris finite() needs ieeefp.h
dnl
AC_CHECK_HEADERS(ieeefp.h)

dnl Check for some functions
AC_CHECK_FUNCS(random drand48)

dnl **************************************************
dnl * ORBit support
dnl **************************************************
GNOMEGNORBA_LIBS="$GNOMEGNORBA_LIBS"
AC_SUBST(GNOMEGNORBA_LIBS)


dnl **************************************************
dnl * Check for Perl
dnl **************************************************
dnl
AC_CHECK_PROG(perl_val, perl, true, false)
if $perl_val; then
  AC_MSG_CHECKING(for perl ExtUtils::Embed module)
  perl -e 'eval { require ExtUtils::Embed }; if ($@) { exit(1); } else { exit(0); }'
  if test "x$?" = "x0"; then
    AC_MSG_RESULT(yes)

    dnl Use ExtUtils::Embed to figure out the other options.
    PERL_CCCDLFLAGS=`perl -MConfig -e 'print $Config{cccdlflags},"\n";'`
    PERL_LDDLFLAGS=`perl -MConfig -e 'print $Config{lddlflags},"\n";'`
    PERL_CC=`perl -MConfig -e 'print $Config{cc},"\n";'`
    PERL_LD=`perl -MConfig -e 'print $Config{ld},"\n";'`
    PERL_CCOPTS=`perl -MExtUtils::Embed -e ccopts`
    PERL_LDOPTS=`perl -MExtUtils::Embed -e ldopts`
    AC_SUBST(PERL_CCCDLFLAGS)
    AC_SUBST(PERL_LDDLFLAGS)
    AC_SUBST(PERL_CC)
    AC_SUBST(PERL_LD)
    AC_SUBST(PERL_CCOPTS)
    AC_SUBST(PERL_LDOPTS)
  else
    AC_MSG_RESULT(no)
    perl_val=false
  fi
fi
AM_CONDITIONAL(WITH_PERL, $perl_val)

dnl **************************************************
dnl * Check for Python
dnl **************************************************
AC_CHECK_PROG(python_val, python, true, false)
if $python_val; then
	  PY_PREFIX=`python -c 'import sys ; print sys.prefix'`
	  PY_EXEC_PREFIX=`python -c 'import sys ; print sys.exec_prefix'`
	  changequote(<<, >>)dnl
	  PY_VERSION=`python -c 'import sys ; print sys.version[0:3]'`
	  changequote([, ])dnl
	  if test -f $PY_PREFIX/include/python$PY_VERSION/Python.h; then
		  PY_LIBS="python$PY_VERSION"
		  PY_LIB_LOC="-L$PY_EXEC_PREFIX/lib/python$PY_VERSION/config"
		  PY_CFLAGS="-I$PY_PREFIX/include/python$PY_VERSION"
		  PY_MAKEFILE="$PY_EXEC_PREFIX/lib/python$PY_VERSION/config/Makefile"
		  PY_LOCALMODLIBS=`sed -n -e 's/^LOCALMODLIBS=\(.*\)/\1/p' $PY_MAKEFILE`
		  PY_BASEMODLIBS=`sed -n -e 's/^BASEMODLIBS=\(.*\)/\1/p' $PY_MAKEFILE`
		  PY_OTHER_LIBS=`sed -n -e 's/^LIBS=\(.*\)/\1/p' $PY_MAKEFILE`
		  PY_EXTRA_LIBS="$PY_LOCALMODLIBS $PY_BASEMODLIBS $PY_OTHER_LIBS"
		  AC_SUBST(PY_LIBS)
		  AC_SUBST(PY_LIB_LOC)
		  AC_SUBST(PY_CFLAGS)
		  AC_SUBST(PY_EXTRA_LIBS)
	  else
		  python_val=false
	  fi
fi
AM_CONDITIONAL(WITH_PYTHON, $python_val)

dnl ******************************
dnl Check for Bonobo
dnl ******************************
try_bonobo=true
bonobo=
bonobo_msg=no
have_bonobo=false
AC_ARG_WITH(bonobo,
	[--{with,without}-bonobo   Compile with Bonobo support or without it],
	if test x$withval = xno; then
		try_bonobo=false
	fi
)

gnumeric_executable=gnumeric
AC_SUBST(gnumeric_executable)
if $try_bonobo; then
	AC_MSG_CHECKING(for Bonobo > 0.4)
	if gnome-config --libs bonobo > /dev/null 2>&1; then
		vers=`gnome-config --modversion bonobo`
		case $vers
		in
		    bonobo-0.[[01234]]) bonobo_ok=false ;;
		    *) bonobo_ok=true ;;
		esac
	else
		bonobo_ok=false
	fi
	
	if $bonobo_ok; then
		AC_MSG_RESULT(found)
		AC_DEFINE(ENABLE_BONOBO)
		have_bonobo=true
		bonobo=bonobo
		bonobo_msg=yes
	        gnumeric_executable=gnumeric-bonobo
	else
		AC_MSG_RESULT(not found)
	fi
fi
AM_CONDITIONAL(BONOBO, $have_bonobo)

dnl ******************************
dnl Kludge for Libole2 virtual link.
dnl ******************************
AM_CONDITIONAL(LIBOLE2_PUBLIC_LIBRARY, false)

dnl ******************************
dnl Check for GB
dnl ******************************
try_gb=true
gb=
gb_msg=no
have_gb=false
AC_ARG_WITH(gb,
	[--{with,without}-gb       Compile with Gb support or without it],
	if test x$withval = xno; then
		try_gb=false
	fi
)

if $try_gb; then
	AC_MSG_CHECKING(for Gb > 0.0)
	if gnome-config --libs gb > /dev/null 2>&1; then
		vers=`gnome-config --modversion gb`
		case $vers
		in
		    gb-0.[[0]]) gb_ok=false ;;
		    *) gb_ok=true ;;
		esac
	else
		gb_ok=false
	fi
	
	if $gb_ok; then
		AC_MSG_RESULT(found)
		AC_DEFINE(ENABLE_GB)
		have_gb=true
		gb=gb
		gb_msg=yes
	else
		AC_MSG_RESULT(not found)
	fi
fi
AM_CONDITIONAL(GB, $have_gb)

dnl ******************************
dnl GnomePrint checking
dnl ******************************
AC_MSG_CHECKING(for GnomePrint libraries)
if gnome-config --libs print > /dev/null 2>&1; then 
    AC_MSG_RESULT(found)
    . `gnome-config --libdir`/printConf.sh
    AC_MSG_CHECKING(for GnomePrint >= 0.8)
    case x$MODULE_VERSION in
	xgnome-print-0.[[01234567]])
		AC_MSG_ERROR(You need at least GNOME print 0.8 for this version of Gnumeric)
		;;
    esac
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(Did not find GnomePrint installed)
fi

dnl ******************************
dnl LibGlade checking
dnl ******************************
AC_MSG_CHECKING(for Glade libraries >= 0.11)
if gnome-config --libs libglade > /dev/null 2>&1; then 
	vers=`gnome-config --modversion libglade`
	case $vers
	in
	    libglade-0.1[[123456789]]) AC_MSG_RESULT(found) ;;
	    *) AC_MSG_ERROR(libglade is too old need >= 0.11) ;;
	esac
else
    AC_MSG_ERROR(Did not find libGlade installed)
fi

EXTRA_GNOME_LIBS=`gnome-config --libs gnomeui print libglade $bonobo $gb`
EXTRA_GNOME_CFLAGS=`gnome-config --cflags gnomeui print libglade $bonobo $gb`
AC_SUBST(EXTRA_GNOME_LIBS)
AC_SUBST(EXTRA_GNOME_CFLAGS)

AM_CONDITIONAL(LIBOLE2_PUBLIC_LIBRARY, false)

AC_OUTPUT([
gnumeric.spec
Makefile
icons/Makefile
src/Makefile
src/dialogs/Makefile
src/widgets/Makefile
src/functions/Makefile
src/portability.h
graph/Makefile
wizards/Makefile
wizards/graphics/Makefile
doc/Makefile
doc/C/Makefile
doc/es/Makefile
corba-test/Makefile
plugins/Makefile
plugins/sample/Makefile
plugins/sc/Makefile
plugins/sylk/Makefile
plugins/excel/Makefile
plugins/excel/libole2/Makefile
plugins/lotus-123/Makefile
plugins/oleo/Makefile
plugins/python/Makefile
plugins/perl/Makefile
plugins/perl/ext/Makefile.PL
plugins/stat/Makefile
plugins/guile/Makefile
plugins/ff-csv/Makefile
plugins/text/Makefile
plugins/xbase/Makefile
plugins/html/Makefile
plugins/dif/Makefile
plugins/ff-stf/Makefile
intl/Makefile
po/Makefile.in
macros/Makefile
gnumeric.desktop
stamp.h
],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])


echo "

Configuration:

	Source code location:	${srcdir}
	Compiler:		${CC} 
	
	Bonobo Support:		${bonobo_msg}
"
dnl	GB Support:		${gb_msg}
