SetCompressor /SOLID lzma

; Script generated by the HM NIS Edit Script Wizard.
;!include PathMani.nsh

; HM NIS Edit Wizard helper defines
!define GNM_NAME		"Gnumeric"
!define GNM_LONGNAME		"Gnumeric Spreadsheet"
!define GNM_APPNAME		"Gnumeric Spreadsheet"
!define GNM_VERSION		"@VERSION@"
!define GNM_VERSION_TAG		"win32-1"
!define GNM_VERSION_FULL	"${GNM_VERSION}-${GNM_VERSION_TAG}"
!define GNM_UNINST_KEY		"Software\Microsoft\Windows\CurrentVersion\Uninstall\${GNM_NAME}"
!define GNM_STARTMENU_REG_VAL	"NSIS:StartMenuDir"

!define GOFFICE_VERSION		"@GOFFICE_VERSION@"

!define GTK_VERSION		"2.10"
!define GTK_VENDORRC		"14"
!define GTK_VENDORVERSION	"${GTK_VERSION}.${GTK_VENDORRC}"

; MUI 1.67 compatible ------
!include "MUI.nsh"

;ReserveFile "op1.ini"
;ReserveFile "op3.ini"
;!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS
;!insertmacro MUI_RESERVEFILE_LANGDLL

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON	"${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON	"${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

!define MUI_WELCOMEPAGE_TITLE_3LINES
!define MUI_WELCOMEPAGE_TEXT "You can always get the newest version from:\r\n\r\nhttp://www.gnumeric.org/\r\n\r\nThis wizard will guide you through the installation of $(^NameDA).\r\n\r\nIt is recommended that you close all other applications before starting Setup. This will make it possible to update relevant system files without having to reboot your computer.\r\n\r\n$_CLICK"
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "gpl.txt"
Page custom CustomPageShortcut
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
var ICONS_GROUP
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "Gnumeric"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_TITLE_3LINES
!define MUI_FINISHPAGE_LINK "Visit the Gnumeric website for the latest news"
!define MUI_FINISHPAGE_LINK_LOCATION "http://www.gnumeric.org/"
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Var SHORTINSTDIR
Var ISADMIN
Var INI_VALUE

Name "${GNM_LONGNAME} ${GNM_VERSION_FULL}"
OutFile			"gnumeric-${GNM_VERSION_FULL}.exe"
;Icon			"gnm-installer.ico"
InstallDir		"C:\Program Files\${GNM_NAME}\${GNM_VERSION}"
InstallDirRegKey	HKLM "Software\GTK\2.0\${GNM_NAME}" "Path"
ShowInstDetails		hide
ShowUnInstDetails	show

Section "Gnumeric (required)" SEC03
  SectionIn RO
  SetOutPath "$INSTDIR"

  File /r /x "libpython*.dll" "bin\*"
  File /r etc lib share

  WriteRegStr SHCTX "Software\GTK\2.0\${GNM_NAME}" "Path" "$INSTDIR"
  WriteRegStr SHCTX "Software\GTK\2.0\${GNM_NAME}" "Version" "$GNM_VERSION_FULL"

  GetFullPathName /SHORT $SHORTINSTDIR $INSTDIR
  StrCmp $ISADMIN "1" 0 +5
  ExecWait "regedit /s $SHORTINSTDIR\etc\win32\reg\gnumeric-dialogs.reg"
  ExecWait "regedit /s $SHORTINSTDIR\etc\win32\reg\gnumeric-general.reg"
  ExecWait "regedit /s $SHORTINSTDIR\etc\win32\reg\gnumeric-plugins.reg"
  Goto +4
  ExecWait "regedit /s $SHORTINSTDIR\etc\win32\reg\gnumeric-dialogs.hkcu.reg"
  ExecWait "regedit /s $SHORTINSTDIR\etc\win32\reg\gnumeric-general.hkcu.reg"
  ExecWait "regedit /s $SHORTINSTDIR\etc\win32\reg\gnumeric-plugins.hkcu.reg"

  ;ExecWait "$SHORTINSTDIR\gtk-update-icon-cache.exe $SHORTINSTDIR\share\icons\gnome"
  ;ExecWait "$SHORTINSTDIR\gtk-update-icon-cache.exe $SHORTINSTDIR\share\icons\hicolor"

  !define Index "Line${__LINE__}"
  ReadRegStr $1 HKCR ".gnumeric" ""
  StrCmp $1 "" "${Index}-NoBackup1"
    StrCmp $1 "Gnumeric.XML" "${Index}-NoBackup1"
    WriteRegStr HKCR ".gnumeric" "Old Default" $1
"${Index}-NoBackup1:"
  ReadRegStr $1 HKCR ".gnumeric" "Content Type"
  StrCmp $1 "" "${Index}-NoBackup2"
    StrCmp $1 "application/x-gnumeric" "${Index}-NoBackup2"
    WriteRegStr HKCR ".gnumeric" "Old Content Type" $1
"${Index}-NoBackup2:"
  WriteRegStr HKCR ".gnumeric" "" "Gnumeric.XML"
  WriteRegStr HKCR ".gnumeric" "Content Type" "application/x-gnumeric"
  ReadRegStr $0 HKCR "Gnumeric.XML" ""
  StrCmp $0 "" 0 "${Index}-Skip"
	WriteRegStr HKCR "Gnumeric.XML" "" "Gnumeric XML File"
	WriteRegStr HKCR "Gnumeric.XML\shell" "" "Open"
	WriteRegStr HKCR "Gnumeric.XML\DefaultIcon" "" "$INSTDIR\gnumeric.exe,0"
"${Index}-Skip:"
  WriteRegStr HKCR "Gnumeric.XML\shell\Open\command" "" '$INSTDIR\gnumeric.exe "%1"'
  !undef Index
SectionEnd

;Section Translations
;  File "*\*\*\*\gnumeric.mo" "*\*\*\*\gnumeric-functions.mo"
;  File "*\*\*\*\goffice-${GOFFICE_VERSION}.mo"
;SectionEnd

Section -AdditionalIcons
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\${GNM_APPNAME}.lnk" "$INSTDIR\gnumeric.exe"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\${GNM_APPNAME} Help.lnk" "$INSTDIR\share\gnumeric\${GNM_VERSION}\doc\C\gnumeric.chm"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  SetOutPath "$INSTDIR"

  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr SHCTX "${GNM_UNINST_KEY}" "DisplayName"			"$(^Name)"
  WriteRegStr SHCTX "${GNM_UNINST_KEY}" "UninstallString"		"$INSTDIR\uninst.exe"
  WriteRegStr SHCTX "${GNM_UNINST_KEY}" "DisplayVersion"		"${GNM_VERSION_FULL}"
  WriteRegStr SHCTX "${GNM_UNINST_KEY}" "${GNM_STARTMENU_REG_VAL}"	"$ICONS_GROUP"
  WriteRegStr SHCTX "${GNM_UNINST_KEY}" "Context" "$ISADMIN"
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} ""
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} ""
!insertmacro MUI_FUNCTION_DESCRIPTION_END

LangString TEXT_IO_TITLE_OP1 ${LANG_ENGLISH} "Install Option"
LangString TEXT_IO_SUBTITLE_OP1 ${LANG_ENGLISH} "You can choose to install ${GNM_NAME} for anyone who uses this computer or only you."

Function CustomPageShortcut
  ClearErrors
  UserInfo::GetName
  IfErrors Win9x
  Pop $0
  UserInfo::GetAccountType
  Pop $1
  StrCmp $1 "Admin" 0 not_admin
Win9x:
  !insertmacro MUI_HEADER_TEXT "$(TEXT_IO_TITLE_OP1)" "$(TEXT_IO_SUBTITLE_OP1)"
  !insertmacro MUI_INSTALLOPTIONS_DISPLAY "op1.ini"
  !insertmacro MUI_INSTALLOPTIONS_READ $INI_VALUE "op1.ini" "Field 2" "State"
  StrCmp $INI_VALUE "1" 0 not_admin
  StrCpy $ISADMIN "1"
  SetShellVarContext all
  Goto done
not_admin:
  StrCpy $ISADMIN "0"
  SetShellVarContext current
done:
FunctionEnd

Function .onInit
  ;InitPluginsDir
;  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "op1.ini"
;  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "op3.ini"
FunctionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Push $1

  ReadRegStr $ISADMIN HKLM "${GNM_UNINST_KEY}" "Context"
  StrCmp $ISADMIN "1" 0 +2
  SetShellVarContext all

  ReadRegStr $ICONS_GROUP SHCTX "${GNM_UNINST_KEY}" "${GNM_STARTMENU_REG_VAL}"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\license.txt"

  Delete "$SMPROGRAMS\$ICONS_GROUP\${GNM_APPNAME}.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\${GNM_APPNAME} Help.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Theme Selector.lnk"
  GetFullPathName /SHORT $SHORTINSTDIR $INSTDIR

  RMDir "$SMPROGRAMS\$ICONS_GROUP"

  RMDir /REBOOTOK "$INSTDIR"

  DeleteRegKey SHCTX "${GNM_UNINST_KEY}"

  ReadRegStr $1 SHCTX "Software\GTK\2.0" "Path"
  StrCmp $1 "$INSTDIR" 0 +2
  DeleteRegValue SHCTX "Software\GTK\2.0" "Path"

  ReadRegStr $1 SHCTX "Software\GTK\2.0" "DllPath"
  StrCmp $1 "$INSTDIR" 0 +2
  DeleteRegValue SHCTX "Software\GTK\2.0" "DllPath"

  ReadRegStr $1 SHCTX "Software\GTK\2.0" "Version"
  StrCmp $1 "${GTK_VERSION}" 0 +2
  DeleteRegValue SHCTX "Software\GTK\2.0" "Version"

  ReadRegStr $1 SHCTX "Software\GTK\2.0" "VendorVersion"
  StrCmp $1 "${GTK_VENDORVERSION}" 0 +2
  DeleteRegValue SHCTX "Software\GTK\2.0" "VendorVersion"

  ReadRegStr $1 SHCTX "Software\GTK\2.0\${GNM_NAME}" "Path"
  StrCmp $1 "$INSTDIR" 0 +2
  DeleteRegValue SHCTX "Software\GTK\2.0\${GNM_NAME}" "Path"

  DeleteRegKey /ifempty SHCTX "Software\GTK\2.0\${GNM_NAME}"
  DeleteRegKey /ifempty SHCTX "Software\GTK\2.0"
  DeleteRegKey /ifempty SHCTX "Software\GTK"

  !define Index "Line${__LINE__}"
  ReadRegStr $1 HKCR ".gnumeric" ""
  StrCmp $1 "Gnumeric.XML" 0 "${Index}-NoOwn"
    ReadRegStr $1 HKCR ".gnumeric" "Old Content Type"
    StrCmp $1 "" 0 "${Index}-Restore1"
      DeleteRegValue HKCR ".gnumeric" "Content Type"
    Goto "${Index}-Next"
"${Index}-Restore1:"
      WriteRegStr HKCR ".gnumeric" "Content Type" $1
      DeleteRegValue HKCR ".gnumeric" "Old Content Type"
"${Index}-Next:"
    ReadRegStr $1 HKCR ".gnumeric" "Old Default"
    StrCmp $1 "" 0 "${Index}-Restore2"
      DeleteRegKey HKCR ".gnumeric"
    Goto "${Index}-NoOwn"
"${Index}-Restore2:"
      WriteRegStr HKCR ".gnumeric" "" $1
      DeleteRegValue HKCR ".gnumeric" "Old Default"
"${Index}-NoOwn:"
  DeleteRegKey HKCR "Gnumeric.XML"
  !undef Index

  SetAutoClose true

  Pop $1
SectionEnd

